plugins {
    id 'com.github.ben-manes.versions' version '0.14.0'
}

apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'com.github.ben-manes.versions'


mainClassName = 'cmanager.Main'
sourceCompatibility = 1.7
targetCompatibility = 1.7
version = '0.3.0'

sourceSets {
    main {
        java {
            srcDir 'src'
        }
    }

    test {
        java {
            srcDirs = ["test"]
        }
    }
}


task Version(type: Copy) {
    outputs.files 'src/cmanager/global/Version.java'
    from('templates') {
        exclude '**/*.properties'
    }
    expand(
        version: version
    )
    into 'src/cmanager/global'
}

compileJava.dependsOn Version

clean.doFirst {
    delete 'src/cmanager/global/Version.java'
}

repositories {
    mavenCentral()
    maven {
        url 'https://josm.openstreetmap.de/nexus/content/groups/public/'
    }
}

dependencies {
    compile group: 'commons-codec', name: 'commons-codec', version: '1.5'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.7'
    compile group: 'joda-time', name: 'joda-time', version: '2.9.9'
    compile group: 'org.openstreetmap.jmapviewer', name: 'jmapviewer', version: '2.4'

    testCompile 'junit:junit:4.12'
}

jar {
    baseName = 'cm'
    manifest { attributes 'Main-Class': mainClassName }

    from('.') {
        include 'ressources/images/*.jpg'
    }

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
