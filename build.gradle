apply plugin: 'application'
apply plugin: 'java'

mainClassName = 'cmanager.Main'
project.ext.ocOkapiPropertiesFile = projectDir.getPath() + "${File.separator}oc_okapi.properties"
sourceCompatibility = 1.7
targetCompatibility = 1.7
version = '0.2.42'

sourceSets {
    main {
        java {
            srcDir 'src'
        }
    }

	test {
        java {
            srcDirs = ["test"]
        }
    }
}

task OcOkapiKeys(type: Copy) {
    assert file(project.ocOkapiPropertiesFile).exists() : project.ocOkapiPropertiesFile + ' file missing. Please follow the instructions in README.md'
    outputs.files 'src/cmanager/OKAPI/OKAPIKeys.java'
    Properties properties = new Properties()
    File propertiesFile = new File(project.ocOkapiPropertiesFile)
    properties.load(propertiesFile.newDataInputStream())
    from('templates') {
        exclude '**/*.properties', 'Version.java'
    }
    expand(
        oc_okapi_de_consumer_key: properties.getProperty('oc_okapi_de_consumer_key'),
        oc_okapi_de_consumer_secret: properties.getProperty('oc_okapi_de_consumer_secret')
    )
    into 'src/cmanager/OKAPI'
}

task Version(type: Copy) {
    outputs.files 'src/cmanager/global/Version.java'
    from('templates') {
        exclude '**/*.properties', 'OKAPIKeys.java'
    }
    expand(
        version: version
    )
    into 'src/cmanager/global'
}

compileJava.dependsOn OcOkapiKeys, Version

clean.doFirst {
    delete 'src/cmanager/OKAPI/OKAPIKeys.java'
    delete 'src/cmanager/global/Version.java'
}

repositories {
    mavenCentral()
    maven {
        url 'https://josm.openstreetmap.de/nexus/content/groups/public/'
    }
}

dependencies {
    compile group: 'commons-codec', name: 'commons-codec', version: '1.10'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'
    compile group: 'joda-time', name: 'joda-time', version: '2.9.7'
    compile group: 'com.github.scribejava', name: 'scribejava-core', version: '4.0.0'
    compile group: 'org.openstreetmap.jmapviewer', name: 'jmapviewer', version: '2.0'

    testCompile 'junit:junit:4.12'
}

jar {
    baseName = 'cm'
    manifest { attributes 'Main-Class': mainClassName }

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
